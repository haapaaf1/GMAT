%  Script Mission - Formation Rendezvous Example 
%
%  This script demonstrates how to set up a formation flying mission that
%  uses targeting sequences. The mission requires spacecraft dependent 
%  coordinate systems and has plots in different spacecraft dependent 
%  coordinate systems and targets variables that are expressed in s/c 
%  dependent coordinate systems.
%
%  REVISION HISTORY
%  $Id: Ex_FormationRendezvous.script,v 1.2 2007/02/26 23:07:34 edove Exp $


% -------------------------------------------------------------------------
% --------------------------- Create Objects ------------------------------
% -------------------------------------------------------------------------


% ----------------------Spacecraft/Propagators-----------------------------

%  Create Sat1
Create Spacecraft Sat1;
GMAT Sat1.Epoch.UTCGregorian = 01 Jan 2000 11:59:27.966;
GMAT Sat1.CoordinateSystem = EarthMJ2000Eq;
GMAT Sat1.StateType = Keplerian;
GMAT Sat1.SMA   = 7600;
GMAT Sat1.ECC   = 0;
GMAT Sat1.INC   = 100.51;
GMAT Sat1.AOP  = 0;
GMAT Sat1.RAAN  = 278.85;
GMAT Sat1.TA  = 90;    

%  Create Sat2
Create Spacecraft Sat2;
GMAT Sat2.Epoch.UTCGregorian = 01 Jan 2000 11:59:27.966;
GMAT Sat2.CoordinateSystem = EarthMJ2000Eq;
GMAT Sat2.StateType = Keplerian;
GMAT Sat2.SMA   = 7600;
GMAT Sat2.ECC   = 0.045;
GMAT Sat2.INC   = 100.47;
GMAT Sat2.AOP   = 180.95;
GMAT Sat2.RAAN  = 284.39;
GMAT Sat2.TA    = 264.90;      

%  Create Sat3
Create Spacecraft Sat3;
GMAT Sat3.Epoch.UTCGregorian = 01 Jan 2000 11:59:27.966;
GMAT Sat3.CoordinateSystem = EarthMJ2000Eq;
GMAT Sat3.StateType = Keplerian;
GMAT Sat3.SMA   = 7600;
GMAT Sat3.ECC   = 0.0702;
GMAT Sat3.INC   = 100.40;
GMAT Sat3.AOP   = 181.54;
GMAT Sat3.RAAN  = 287.90;
GMAT Sat3.TA    = 261.81;      

%  Create Sat4
Create Spacecraft Sat4;
GMAT Sat4.Epoch.UTCGregorian = 01 Jan 2000 11:59:27.966;
GMAT Sat4.CoordinateSystem = EarthMJ2000Eq;
GMAT Sat4.StateType = Keplerian;
GMAT Sat4.SMA   = 7600;
GMAT Sat4.ECC   = 0.101;
GMAT Sat4.INC   = 100.29;
GMAT Sat4.AOP   = 182.141;
GMAT Sat4.RAAN  = 291.82;
GMAT Sat4.TA    = 258.67;      

%  Define Force Model with Exponential drag
Create ForceModel LowEarth;
GMAT LowEarth.PointMasses          = {Earth, Sun, Luna, Jupiter}; 


% Create Propagator
Create Propagator RKV_LowEarth;
GMAT RKV_LowEarth.Type    = RungeKutta89;
GMAT RKV_LowEarth.MinStep = .001;
GMAT RKV_LowEarth.MaxStep = 900;
GMAT RKV_LowEarth.FM      = LowEarth;

%  Create maneuver
Create ImpulsiveBurn Burn1;
GMAT Burn1.Axes = VNB;
GMAT Burn1.Element1        = .0001;

%  Create Differential Corrector
Create DifferentialCorrector DC;

% ------------------------------Coordinate Systems-------------------------

%  Create Sat1LVLH coordinate system
Create CoordinateSystem Sat1LVLH;
GMAT Sat1LVLH.Axes    = ObjectReferenced;
GMAT Sat1LVLH.Origin  = Sat1;
GMAT Sat1LVLH.Primary = Earth;
GMAT Sat1LVLH.YAxis   = -N;
GMAT Sat1LVLH.ZAxis   = -R;


%  Create Sat2VNB coordinate system
Create CoordinateSystem Sat2VNB;
GMAT Sat2VNB.Axes    = ObjectReferenced;
GMAT Sat2VNB.Origin  = Sat2;
GMAT Sat2VNB.Primary = Earth;
GMAT Sat2VNB.XAxis   = V;
GMAT Sat2VNB.YAxis   = N;

% ------------------------------XYPlots--------------------------------------
%  Create XYPlot of Sat2 in Sat1LVLH system
Create XYPlot Sat2wrtSat1LVLH;
GMAT Sat2wrtSat1LVLH.TargetStatus = Off;
GMAT Sat2wrtSat1LVLH.IndVar   = Sat2.Sat1LVLH.X;
GMAT Sat2wrtSat1LVLH.Add      = Sat2.Sat1LVLH.Z;
GMAT Sat2wrtSat1LVLH.Grid     = On
%

%  Create XYPlot Sat1 in Sat2 VNB system
Create XYPlot Sat1wrtSat2VNB;
GMAT Sat1wrtSat2VNB.TargetStatus = Off;
GMAT Sat1wrtSat2VNB.IndVar       = Sat1.Sat2VNB.X;
GMAT Sat1wrtSat2VNB.Add          = Sat1.Sat2VNB.Y;
GMAT Sat1wrtSat2VNB.Grid         = On;

%  Create XYPlot Sat1 in Sat2 VNB system
Create XYPlot Sat1wrtSat2VNB2;
GMAT Sat1wrtSat2VNB2.TargetStatus = Off;
GMAT Sat1wrtSat2VNB2.IndVar       = Sat1.Sat2VNB.Y;
GMAT Sat1wrtSat2VNB2.Add          = Sat1.Sat2VNB.Z;
GMAT Sat1wrtSat2VNB2.Grid         = On;

% ------------------------------OpenGL Plots--------------------------------------

Create OpenGLPlot Sat2InSat1LVLH;
GMAT Sat2InSat1LVLH.Add = {Sat2, Sat3, Sat4};
GMAT Sat2InSat1LVLH.CoordinateSystem = Sat1LVLH;
GMAT Sat2InSat1LVLH.ViewPointRef = Earth;
GMAT Sat2InSat1LVLH.ViewPointVector = Vector;
GMAT Sat2InSat1LVLH.ViewDirection = Earth;
GMAT Sat2InSat1LVLH.ViewScaleFactor = .15;
GMAT Sat2InSat1LVLH.FixedFovAngle = 45;
GMAT Sat2InSat1LVLH.ViewUpCoordinateSystem = Sat1LVLH;
GMAT Sat2InSat1LVLH.ViewUpAxis = X;
GMAT Sat2InSat1LVLH.CelestialPlane = Off;
GMAT Sat2InSat1LVLH.XYPlane = On;
GMAT Sat2InSat1LVLH.WireFrame = Off;
GMAT Sat2InSat1LVLH.TargetStatus = On;
GMAT Sat2InSat1LVLH.Axes = Off;
GMAT Sat2InSat1LVLH.EarthSunLines = Off;
GMAT Sat2InSat1LVLH.PerspectiveMode = Off;
GMAT Sat2InSat1LVLH.UseFixedFov = Off;
GMAT Sat2InSat1LVLH.DataCollectFrequency = 1;
GMAT Sat2InSat1LVLH.UpdatePlotFrequency = 50;
GMAT Sat2InSat1LVLH.NumPointsToRedraw = 0;

% ------------------------------ Reports --------------------------------------

Create ReportFile DataReport;
GMAT DataReport.Filename = ./output/SampleMissions/Ex_FormationRendezvous.report;
GMAT DataReport.Precision = 16;
GMAT DataReport.WriteHeaders = On;
GMAT DataReport.ColumnWidth = 20;

% -------------------------------------------------------------------------
% --------------------------- Mission Sequence-----------------------------
% -------------------------------------------------------------------------

%  Toggle plots
%Toggle Sat2wrtSat1RBN On;
Toggle Sat1wrtSat2VNB On;
Toggle Sat2InSat1LVLH On;
Toggle Sat1wrtSat2VNB2 On;


%  Propagate and watch cool plots!!
Report DataReport Sat1.X Sat1.Y Sat1.Z Sat1.VX Sat1.VY Sat1.VZ;
GMAT DataReport.WriteHeaders = Off;
Report DataReport Sat2.X Sat2.Y Sat2.Z Sat2.VX Sat2.VY Sat2.VZ;
Report DataReport Sat3.X Sat3.Y Sat3.Z Sat3.VX Sat3.VY Sat3.VZ;
Report DataReport Sat4.X Sat4.Y Sat4.Z Sat4.VX Sat4.VY Sat4.VZ;

Propagate RKV_LowEarth(Sat1, Sat2, Sat3, Sat4, {Sat1.ElapsedDays = .2});

Report DataReport Sat1.X Sat1.Y Sat1.Z Sat1.VX Sat1.VY Sat1.VZ;
Report DataReport Sat2.X Sat2.Y Sat2.Z Sat2.VX Sat2.VY Sat2.VZ;
Report DataReport Sat3.X Sat3.Y Sat3.Z Sat3.VX Sat3.VY Sat3.VZ;
Report DataReport Sat4.X Sat4.Y Sat4.Z Sat4.VX Sat4.VY Sat4.VZ;

%  Target for rendezvous
Target DC {SolveMode = Solve};

     Vary DC(Burn1.V = 0.0001, {Perturbation = 0.0001, MaxStep = 0.05, Lower = -0.3, Upper = 0.3});
     Vary DC(Burn1.N = 0.000,  {Perturbation = 0.0001, MaxStep = 0.05, Lower = -0.3, Upper = 0.3});
     Vary DC(Burn1.B = 0.000,  {Perturbation = 0.0001, MaxStep = 0.05, Lower = -0.3, Upper = 0.3});
     
     Maneuver Burn1(Sat2);
     
     Propagate  RKV_LowEarth(Sat1, Sat2, Sat3, Sat4, {Sat1.ElapsedSecs = 5498.65387429});
     
     Achieve DC(Sat2.Sat1LVLH.X = 0, {Tolerance = 0.0001});
%     Achieve DC(Sat2.Sat1LVLH.Y = 0, {Tolerance = 0.0001});
     Achieve DC(Sat2.Sat1LVLH.Z = 0, {Tolerance = 0.0001});
     
EndTarget;

Report DataReport Sat1.X Sat1.Y Sat1.Z Sat1.VX Sat1.VY Sat1.VZ;
Report DataReport Sat2.X Sat2.Y Sat2.Z Sat2.VX Sat2.VY Sat2.VZ;
Report DataReport Sat3.X Sat3.Y Sat3.Z Sat3.VX Sat3.VY Sat3.VZ;
Report DataReport Sat4.X Sat4.Y Sat4.Z Sat4.VX Sat4.VY Sat4.VZ;

%  Target for rendezvous
Target DC {SolveMode = RunInitialGuess};

     Vary DC(Burn1.V = 0.0001, {Perturbation = 0.0001, MaxStep = 0.5, Lower = -1.0, Upper = 1.0});
%     Vary DC(Burn1.N = 0.000,  {Perturbation = 0.0001, MaxStep = 0.5, Lower = -1.0, Upper = 1.0});
     Vary DC(Burn1.B = 0.000,  {Perturbation = 0.0001, MaxStep = 0.5, Lower = -1.0, Upper = 1.0});
     
     Maneuver Burn1(Sat2);
     
     Propagate  RKV_LowEarth(Sat1, Sat2, Sat3, Sat4, {Sat1.ElapsedSecs = 5498.65387429});
     
     Achieve DC(Sat2.VX = Sat1.VX, {Tolerance = 0.0001});
     Achieve DC(Sat2.VY = Sat1.VY, {Tolerance = 0.0001});
     Achieve DC(Sat2.VZ = Sat1.VZ, {Tolerance = 0.0001});
     
EndTarget;

Report DataReport Sat1.X Sat1.Y Sat1.Z Sat1.VX Sat1.VY Sat1.VZ;
Report DataReport Sat2.X Sat2.Y Sat2.Z Sat2.VX Sat2.VY Sat2.VZ;
Report DataReport Sat3.X Sat3.Y Sat3.Z Sat3.VX Sat3.VY Sat3.VZ;
Report DataReport Sat4.X Sat4.Y Sat4.Z Sat4.VX Sat4.VY Sat4.VZ;

%  Propagate and watch cool plots!!
Propagate RKV_LowEarth(Sat1, Sat2, Sat3, Sat4, {Sat1.ElapsedDays = .2});

Report DataReport Sat1.X Sat1.Y Sat1.Z Sat1.VX Sat1.VY Sat1.VZ;
Report DataReport Sat2.X Sat2.Y Sat2.Z Sat2.VX Sat2.VY Sat2.VZ;
Report DataReport Sat3.X Sat3.Y Sat3.Z Sat3.VX Sat3.VY Sat3.VZ;
Report DataReport Sat4.X Sat4.Y Sat4.Z Sat4.VX Sat4.VY Sat4.VZ;