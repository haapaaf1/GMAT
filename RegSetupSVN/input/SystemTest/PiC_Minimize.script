% $Id: PiC_Minimize.script,v 1.4 2007/07/26 19:13:13 edove Exp $

%  This script tests the Minimize command, specifically, that it can
%  handle variables, array elements, and spacecraft parameters

%-----------------------------------------------------------------
%---------------------- Create Spacecraft ------------------------
%-----------------------------------------------------------------
Create Spacecraft Sat;
GMAT Sat.DisplayStateType = Cartesian;
%  Do not change these, they are used later in the tests and need
%  to have these specific values!!!
GMAT Sat.VX         = 0;
GMAT Sat.VY         = 8;
GMAT Sat.VZ         = 10;

%-----------------------------------------------------------------
%-----------------Create and Setup the Optimizer------------------
%-----------------------------------------------------------------
Create FminconOptimizer SQPfmincon
GMAT SQPfmincon.DiffMaxChange = 0.01;      % Real number
GMAT SQPfmincon.DiffMinChange = 0.000001;  % Real number
GMAT SQPfmincon.MaxFunEvals   = 1000;    % Real number
GMAT SQPfmincon.MaxIter       = 250;     % Real number
GMAT SQPfmincon.TolX          = 0.01;    % Real number
GMAT SQPfmincon.TolFun        = 0.0001;  % Real number
GMAT SQPfmincon.TolCon        = 0.0001;  % Real number
GMAT SQPfmincon.DerivativeCheck = Off;   % {On, Off}
GMAT SQPfmincon.Diagnostics     = On;    % {On, Off}
GMAT SQPfmincon.Display         = Iter   % {Iter, Off, Notify, Final}
GMAT SQPfmincon.GradObj         = Off;   % {On, Off}
GMAT SQPfmincon.GradConstr      = Off;   % {On, Off}

Create ReportFile PiC_Minimize;
PiC_Minimize.Filename = ./output/SystemTest/PiC_Minimize.report;
PiC_Minimize.WriteHeaders = Off;

%-----------------------------------------------------------------
%-----------------Create Variable and Arrays ---------------------
%-----------------------------------------------------------------
Create Variable X1 X2 F G TestNumber Flag;
Create Array AF[2,2];

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%-----------------------------------------------------------------
%----------------- Run the Optimization Loop ---------------------
%-----------------------------------------------------------------
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%-----------------
%----Test 1-------
%-----------------
%  Test using a variable in the minimize command

Optimize SQPfmincon;

    %  Vary the independent variables
    Vary SQPfmincon( Sat.X = 0  , { Upper = 10, Lower = -10 } );
    Vary SQPfmincon( Sat.Y = 0  , { Upper = 10, Lower = -10 } );
    GMAT X1 = Sat.X;
    GMAT X2 = Sat.Y;
   
    %  The cost function and Minimize command
    GMAT F =  ( X1 - 2 )^2 + ( X2 - 2 )^2 
    Minimize SQPfmincon(F);

EndOptimize;

Flag = 1;
If X1 < 1.9999;  
   Flag = 0;
EndIf;
If X1  > 2.0001;
   Flag = 0;
EndIf;

Report PiC_Minimize Flag X1 X2;

%%-----------------
%%----Test 2-------
%%-----------------
%  Test using an array element in a minimize command

Optimize SQPfmincon;

    %  Vary the independent variables
    Vary SQPfmincon( Sat.X = 0  , { Upper = 10, Lower = -10 } );
    Vary SQPfmincon( Sat.Y = 0  , { Upper = 10, Lower = -10 } );
    GMAT X1 = Sat.X;
    GMAT X2 = Sat.Y;
   
    %  The cost function and Minimize command
    GMAT AF(2,2) =  ( X1 - 2 )^2 + ( X2 - 2 )^2 
    Minimize SQPfmincon(AF(2,2));

EndOptimize;

Flag = 1;
If X1 < 1.9999;  
   Flag = 0;
EndIf;
If X1  > 2.0001;
   Flag = 0;
EndIf;

Report PiC_Minimize Flag X1 X2;


%%!!!!!!!!!!!!!!!!!!!!!  Test 3 Currently fails
%%-----------------
%%----Test 3-------
%%-----------------
%  Test using a s/c parameter in Minimize command

Optimize SQPfmincon;

    %  Vary the independent variables
    Vary SQPfmincon( Sat.X = 0  , { Upper = 10, Lower = -10 } );
    Vary SQPfmincon( Sat.Y = 0  , { Upper = 10, Lower = -10 } );
    GMAT X1 = Sat.X;
    GMAT X2 = Sat.Y;
   
    %  The cost function and Minimize command
    GMAT Sat.Z =  ( X1 - 2 )^2 + ( X2 - 2 )^2 
    Minimize SQPfmincon(Sat.Z);

EndOptimize;

Flag = 1;
If X1 < 1.9999;  
   Flag = 0;
EndIf;
If X1  > 2.0001;
   Flag = 0;
EndIf;

Report PiC_Minimize Flag X1 X2;
