%$Id$
%  Build 4, Test Case 3.  This is a formation flying mission.  The mission 
%  requires spacecraft dependent coordinate systems and has plots in different 
%  spacecraft dependent coordinate systems and targets variables that are 
%  expressed in s/c dependent coordinate systems.

% -------------------------------------------------------------------------
% --------------------------- Create Objects ------------------------------
% -------------------------------------------------------------------------


% ----------------------Spacecraft/Propagators-----------------------------

%  Create Sat1
Create Spacecraft Sat1;
GMAT Sat1.Epoch.UTCGregorian = 01 Jan 2000 11:59:27.966;
GMAT Sat1.CoordinateSystem = EarthMJ2000Eq;
GMAT Sat1.DisplayStateType = Keplerian;
GMAT Sat1.SMA   = 7600;
GMAT Sat1.ECC   = 0;
GMAT Sat1.INC   = 100.51;
GMAT Sat1.AOP  = 0;
GMAT Sat1.RAAN  = 278.85;
GMAT Sat1.TA  = 90;    

%  Create Sat2
Create Spacecraft Sat2;
GMAT Sat2.Epoch.UTCGregorian = 01 Jan 2000 11:59:27.966;
GMAT Sat2.CoordinateSystem = EarthMJ2000Eq;
GMAT Sat2.DisplayStateType = Keplerian;
GMAT Sat2.SMA   = 7600;
GMAT Sat2.ECC   = 0.045;
GMAT Sat2.INC   = 100.47;
GMAT Sat2.AOP   = 180.95;
GMAT Sat2.RAAN  = 284.39;
GMAT Sat2.TA    = 264.90;      

%  Create Sat3
Create Spacecraft Sat3;
GMAT Sat3.Epoch.UTCGregorian = 01 Jan 2000 11:59:27.966;
GMAT Sat3.CoordinateSystem = EarthMJ2000Eq;
GMAT Sat3.DisplayStateType = Keplerian;
GMAT Sat3.SMA   = 7600;
GMAT Sat3.ECC   = 0.0702;
GMAT Sat3.INC   = 100.40;
GMAT Sat3.AOP   = 181.54;
GMAT Sat3.RAAN  = 287.90;
GMAT Sat3.TA    = 261.81;      

%  Create Sat4
Create Spacecraft Sat4;
GMAT Sat4.Epoch.UTCGregorian = 01 Jan 2000 11:59:27.966;
GMAT Sat4.CoordinateSystem = EarthMJ2000Eq;
GMAT Sat4.DisplayStateType = Keplerian;
GMAT Sat4.SMA   = 7600;
GMAT Sat4.ECC   = 0.101;
GMAT Sat4.INC   = 100.29;
GMAT Sat4.AOP   = 182.141;
GMAT Sat4.RAAN  = 291.82;
GMAT Sat4.TA    = 258.67;      

%  Define Force Model with Exponential drag
Create ForceModel LowEarth;
%GMAT LowEarth.Origin               = Earth;
GMAT LowEarth.PointMasses          = {Earth, Sun, Luna, Jupiter}; 


% Create Propagator
Create Propagator RKV_LowEarth;
GMAT RKV_LowEarth.Type    = RungeKutta89;
GMAT RKV_LowEarth.MinStep = .001;
GMAT RKV_LowEarth.MaxStep = 900;
GMAT RKV_LowEarth.FM      = LowEarth;

%  Create maneuver
Create ImpulsiveBurn Burn1;
GMAT Burn1.Axes = VNB;
GMAT Burn1.Element1        = .0001;

%  Create Differential Corrector
Create DifferentialCorrector DC;
GMAT DC.ReportFile = FormationRendezvous-DC.data;
%GMAT DC.MaxIterations = 25;

% ------------------------------Coordinate Systems-------------------------

%  Create Sat1LVLH coordinate system
Create CoordinateSystem Sat1LVLH;
GMAT Sat1LVLH.Axes    = ObjectReferenced;
GMAT Sat1LVLH.Origin  = Sat1;
GMAT Sat1LVLH.Primary = Earth;
GMAT Sat1LVLH.Secondary = Sat1;
GMAT Sat1LVLH.YAxis   = -N;
GMAT Sat1LVLH.ZAxis   = -R;
%



%%  Create Sat2VNB coordinate system
Create CoordinateSystem Sat2VNB;
GMAT Sat2VNB.Axes    = ObjectReferenced;
GMAT Sat2VNB.Origin  = Sat2;
GMAT Sat2VNB.Primary = Earth;
GMAT Sat2VNB.Secondary = Sat2;
GMAT Sat2VNB.XAxis   = V;
GMAT Sat2VNB.YAxis   = N;

% ------------------------------ReortFile------------------------------------
Create ReportFile rf;
GMAT rf.Filename = APT_FormationRendezvous.report;

% ------------------------------XYPlots--------------------------------------
%  Create XYPlot of Sat2 in Sat1LVLH system
Create XYPlot Sat2wrtSat1LVLH;
GMAT Sat2wrtSat1LVLH.SolverIterations = None;
GMAT Sat2wrtSat1LVLH.IndVar   = Sat2.Sat1LVLH.X;
GMAT Sat2wrtSat1LVLH.Add      = Sat2.Sat1LVLH.Z;
GMAT Sat2wrtSat1LVLH.Grid     = On
%

%  Create XYPlot Sat1 in Sat2 VNB system
Create XYPlot Sat1wrtSat2VNB;
GMAT Sat1wrtSat2VNB.SolverIterations = None;
GMAT Sat1wrtSat2VNB.IndVar       = Sat1.Sat2VNB.X;
GMAT Sat1wrtSat2VNB.Add          = Sat1.Sat2VNB.Y;
GMAT Sat1wrtSat2VNB.Grid         = On;

%  Create XYPlot Sat1 in Sat2 VNB system
Create XYPlot Sat1wrtSat2VNB2;
GMAT Sat1wrtSat2VNB2.SolverIterations = None;
GMAT Sat1wrtSat2VNB2.IndVar       = Sat1.Sat2VNB.Y;
GMAT Sat1wrtSat2VNB2.Add          = Sat1.Sat2VNB.Z;
GMAT Sat1wrtSat2VNB2.Grid         = On;
%
%% ------------------------------OpenGL Plots--------------------------------------
%
Create OpenGLPlot Sat2InSat1LVLH;
GMAT Sat2InSat1LVLH.Add = {Sat2, Sat3, Sat4};
GMAT Sat2InSat1LVLH.CoordinateSystem = Sat1LVLH;
GMAT Sat2InSat1LVLH.ViewPointReference = Earth;
GMAT Sat2InSat1LVLH.ViewPointVector = [0, 0, 20000];
GMAT Sat2InSat1LVLH.ViewDirection = Earth;
GMAT Sat2InSat1LVLH.ViewScaleFactor = 0.1;
GMAT Sat2InSat1LVLH.FixedFovAngle = 45;
GMAT Sat2InSat1LVLH.ViewUpCoordinateSystem = EarthMJ2000Eq;
GMAT Sat2InSat1LVLH.ViewUpAxis = Z;
GMAT Sat2InSat1LVLH.CelestialPlane = Off;
GMAT Sat2InSat1LVLH.XYPlane = On;
GMAT Sat2InSat1LVLH.WireFrame = Off;
GMAT Sat2InSat1LVLH.SolverIterations = None;
GMAT Sat2InSat1LVLH.Axes = On;
GMAT Sat2InSat1LVLH.SunLine = Off;
GMAT Sat2InSat1LVLH.PerspectiveMode = Off;
GMAT Sat2InSat1LVLH.UseFixedFov = Off;
GMAT Sat2InSat1LVLH.DataCollectFrequency = 1;
GMAT Sat2InSat1LVLH.UpdatePlotFrequency = 50;
GMAT Sat2InSat1LVLH.NumPointsToRedraw = 0;

Create Array arr1[1,1]

% -------------------------------------------------------------------------
% --------------------------- Mission Sequence-----------------------------
% -------------------------------------------------------------------------

BeginMissionSequence;

%  Toggle plots
%%%Toggle Sat2wrtSat1RBN On;
Toggle Sat1wrtSat2VNB On;
Toggle Sat1wrtSat2VNB2 On;
Toggle Sat2InSat1LVLH On;

%  Propagate and watch cool plots!!

Report rf Sat2.A1ModJulian Sat2.Sat1LVLH.X Sat2.Sat1LVLH.Y Sat2.Sat1LVLH.Z;

Propagate RKV_LowEarth(Sat1, Sat2, Sat3, Sat4, {Sat1.ElapsedDays = .2});
GMAT rf.WriteHeaders = Off;
Report rf Sat2.A1ModJulian Sat2.Sat1LVLH.X Sat2.Sat1LVLH.Y Sat2.Sat1LVLH.Z;

%  Target for rendezvous
Target DC;

     Vary DC(Burn1.Element1 = 0.0001, {Perturbation = 0.0001, MaxStep = 0.05, Lower = -0.3, Upper = 0.3});
     Vary DC(Burn1.Element2 = 0.000,  {Perturbation = 0.0001, MaxStep = 0.05, Lower = -0.3, Upper = 0.3});
     Vary DC(Burn1.Element3 = 0.000,  {Perturbation = 0.0001, MaxStep = 0.05, Lower = -0.3, Upper = 0.3});
     
     Maneuver Burn1(Sat2);
     
     Propagate  RKV_LowEarth(Sat1, Sat2, Sat3, Sat4, {Sat1.ElapsedSecs = 5498.65387429});
     
     Achieve DC(Sat2.Sat1LVLH.X = 0, {Tolerance = 0.0001});
     Achieve DC(Sat2.Sat1LVLH.Y = 0, {Tolerance = 0.0001});
     Achieve DC(Sat2.Sat1LVLH.Z = arr1(1,1), {Tolerance = 0.0001});
     
EndTarget;

Report rf Sat2.A1ModJulian Sat2.Sat1LVLH.X Sat2.Sat1LVLH.Y Sat2.Sat1LVLH.Z;

%  Target for rendezvous
Target DC;

     Vary DC(Burn1.Element1 = 0.0001, {Perturbation = 0.0001, MaxStep = 0.5, Lower = -1.0, Upper = 1.0});
     Vary DC(Burn1.Element2 = 0.000,  {Perturbation = 0.0001, MaxStep = 0.5, Lower = -1.0, Upper = 1.0});
     Vary DC(Burn1.Element3 = 0.000,  {Perturbation = 0.0001, MaxStep = 0.5, Lower = -1.0, Upper = 1.0});
     
     Maneuver Burn1(Sat2);
     
     Propagate  RKV_LowEarth(Sat1, Sat2, Sat3, Sat4, {Sat1.ElapsedSecs = 5498.65387429});
     
     Achieve DC(Sat2.VX = Sat1.VX, {Tolerance = 0.0001});
     Achieve DC(Sat2.VY = Sat1.VY, {Tolerance = 0.0001});
     Achieve DC(Sat2.VZ = Sat1.VZ, {Tolerance = 0.0001});
     
EndTarget;

Report rf Sat2.A1ModJulian Sat2.Sat1LVLH.X Sat2.Sat1LVLH.Y Sat2.Sat1LVLH.Z;
     
%  Propagate and watch cool plots!!
Propagate RKV_LowEarth(Sat1, Sat2, Sat3, Sat4, {Sat1.ElapsedDays = .2});

Report rf Sat2.A1ModJulian Sat2.Sat1LVLH.X Sat2.Sat1LVLH.Y Sat2.Sat1LVLH.Z;
